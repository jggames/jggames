10 GOTO 10000
1000 PMODE 4,1:PCLS:SCREEN 1,1
1010 FORI=1TO75STEP2:CIRCLE(256,30),I,1,.9:NEXT
1020 PMODE3,1
1030 FORI=0TO3STEP3:FORJ=-18TO18:COLORI,I:LINE(215+J,60-SQR(324-J*J)/4)-(215+J,60+SQR(324-J*J)/4),PSET
1040 NEXT:NEXT
1050 FORR=1TO128STEP5:CIRCLE(128,185),R,2,.1:NEXT
1070 LINE(0,187)-(255,187),PSET
1080 PAINT(0,190),3
1090 PMODE4,1:FORI=1TO300:PSET(RND(256)-1,RND(192)-1,1):NEXT
1095 IFRF THEN1600ELSERF=-1
1100 DIMST(5,5):GET(0,0)-(20,20),ST,G
1110 ME=5:ES=0:EF=0:EX=0:EY=0:ED=0:EL=0:EA=0:EB=0
1120 DIME$(ME)
1130 ES$(1)="S4;R16;G8;H8"
1140 ES$(2)="S4;BR7;R4;G5;R3;G5;R3;G6;U3EUL2;E4L4E7"
1150 ES$(3)="S4;BR3R5F3D5;G3L5H3U5E3;BD3R1;BR4L1;BD3NL3;FL5"
1160 ES$(4)="S4;R4F3RE3R4D4R1;G3DF3D4L4H3LG3L4U4;E3UH3U4"
1170 ES$(5)="S4;NR15FNR13FNR11;FNR9FNR7D8F3RE3;U8;BD3BL2;D4U2L3NU2D2"
1200 PX=0:PY=174:PA=0
1210 PS$="S4;NU4RNU4RNU8;R2NU12R3NU16R3NU12R2NU8R2NU4RU4"
1300 SC=0:LV=1:MP=255:BU=0
1400 SA$="B;P100;A;P100;G;P100;F;P100;E;P100;D;P100;C"
1410 SB$="CDEFGAB"
1420 SC$="O1;CDECDECDECDE;O4"
1500 PLAY"T255;L255;O4"
1510 DIMMO$(14):FORI=1TO14:READMO$(I):NEXT
1520 DATAAMALTHEA,IO,EUROPA,GANYMEDE,CALLISTO,LEDA,HIMALIA,LYSITHEA,MACKIE,ELARA,ANAKE,CARME,PASIPHAE,SINOPE
1600 RETURN
2000 CLS:PRINT@8,"MOONS OF JUPITER":PRINT@40,"================"
2010 PRINT:PRINT"MINE ALL THE URNAIUM FROM THE   MOONS OF JUPITER BEFORE THE ALIENS CAN LAND AND CAUSE A"
2020 PRINT:PRINT"CHAIN REACTION THAT COULD DESTROY THE SOLAR SYSTEM."
2030 PRINT:PRINT"MOVE LEFT AND RIGHT AND SHOOT THE ALIENS, OR GO UNDER JUPITER AND MINE THE URANIUM."
2040 PRINT:PRINT"   (K)KEYBOARD OR (J)OYSTICK?"
2050 A$=INKEY:IFA$<>"K"ANDA$<>"J"THEN2050
2060 IFA$="K"THEN2100
2070 PRINT:PRINT"USE THE RIGHT JOYSTICK TO MOVE    AND THE BUTTON TO FIRE/MINE."
2080 KB=0
2090 GOTO2200
2100 PRINT:PRINT"USE THE ARROW KEYS TO MOVE  AND THE SPACE BAR TO FIRE/MINE."
2110 KB=-1
2200 PRINT:PRINT"   PRESS ANY KEY TO BEGIN..."
2210 A$=INKEY$:IFA$=""THEN2210
2220 RETURN
2500 CLS:PRINT@100,"YOU ARE ON MOON";LV;
2510 PRINT@170,MO$(LV);
2520 PRINT@230,"  YOUR SCORE IS:";SC;
2530 PRINT@322,"GET READY TO START MINING!";:PRINT@386,"(30 SECONDS)";
2540 PCLS0:GOSUB1010
2550 RETURN
3000 REM       GAME
3010 IFNOTKB THEN 3100
3015 PX=PX+DP:IFPX<1ORPX>180THENPX=PX-DP:DP=-DP
3020 A$=INKEY$:IFA$<>CHR$(8)THENIFA$<>CHR$(9)THENIFA$<>" "THEN3300
3030 IFA$=CHR$(8)THENDP=-4:GOTO3300
3040 IFA$=CHR$(9)THENDP=4:GOTO3300
3050 IFA$<>" "THEN3300
3060 GOTO3120
3100 PX=INT(PX-(PX-JOYSTICK(0)*3)/5)
3110 BU=PEEK(65280):IFBU=127ORBU=255ORMF THENMP=0:GOTO3300
3120 MF=-1
3130 IFPA<175THEN3200
3140 PLAYSB$
3150 FORI=1TO13-LV:LINE(MP,187)-(MP,191),PRESET
3160 MP=MP-1:IFMP=0THEN4000
3170 NEXT:SC=SC+1
3180 GOTO3300
3200 LINE(PA+8,PY-16)-(PA+8,0),PSET
3210 PLAYSA$
3220 LINE(PA+8,PY-16)-(PA+8,0),PRESET
3230 IFNOTEF OR((PA+8)<(EA+3)OR(PA+8)>(EA+9))THEN3300
3240 PLAYSC$
3250 FORI=1TO9:PUT(EA+9-I,EB+9-I)-(EA+10+I,EB+10+I),ST,PSET
3260 NEXTI
3270 SC=SC+ES*10
3280 EF=0
3300 IFNOT(EF)THENIFRND(5)>2THEN3400
3310 IFEF THEN3340
3320 ES=RND(5):EX=RND(160):EY=0:EA=0:EB=0:ED=RND(11)-6:EL=RND(15)
3330 EF=-1
3340 EX=EX+ED:IFEX<0OREX>160THENEX=EX-ED:ED=-ED
3350 EL=EL-1:IFEL=0THENED=RND(11)-6:EL=RND(15)
3360 EY=INT(EY+LV*2):IFEY>=180THEN4500
3370 DRAW"BM"+STR$(EA)+","+STR$(EB)+";C0;"+ES$(ES)
3380 DRAW"BM"+STR$(EX)+","+STR$(EY)+";C1;"+ES$(ES)
3390 EA=EX:EB=EY
3400 DRAW"BM"+STR$(PA)+","+STR$(PY)+";C0;"+PS$
3410 DRAW"BM"+STR$(PX)+","+STR$(PY)+";C1;"+PS$
3420 PA=PX
3430 PSET(RND(175)-1,RND(175)-1)
3440 GOTO3000
4000 REM    YOU DID IT!
4010 FORI=PY TO0STEP-4:DRAW"BM"+STR$(PA)+","+STR$(I+4)+";C0;"+PS$
4020 DRAW"BM"+STR$(PA)+","+STR$(I)+";C1;"+PS$
4030 PLAY"O1;"+STR$(RND(12)):NEXT
4040 PLAY"O4":LV=LV+1:IFLV=15THENLV=1
4050 GOSUB2500
4060 MP=255:EF=0
4070 SCREEN1,1:GOTO3000
4500 REM      BOOM!
4510 PLAY"O1"
4520 FORI=1TO100
4530 LINE(PA+8,PY)-(RND(256)-1,RND(192)-1),PSET
4540 PLAYSTR$(RND(12))
4550 NEXT
4560 PLAY"O4"
4570 RETURN
5000 FORDE=1TO1500:NEXCT
5010 CLS:PRINT:PRINT"     WELL, SO MUCH FOR THE SOLAR":PRINT"     SYSTEM!"
5020 PRINT:PRINT:PRINT"     YOU MADE IT TO MOON";LV
5025 PRINT:PRINTTAB(10)"("MO$(LV)")";
5030 PRINT:PRINT:PRINT"     WITH A SCORE OF:";SC
5040 PRINT:PRINT:PRINT"        ANOTHER GAME?"
5050 A$=INKEY$:IFA$<>"Y"THENIFA$<>"N"THEN5050
5060 IFA$="N"THENCLS:END
5065 LV=1:EF=0:SC=0:MP=255
5070 CLS0:RETURN
10000 GOSUB1000
10010 GOSUB2000
10020 GOSUB2500
10030 SCREEN1,1:GOSUB3000
10040 GOSUB5000
10050 GOTO10000
