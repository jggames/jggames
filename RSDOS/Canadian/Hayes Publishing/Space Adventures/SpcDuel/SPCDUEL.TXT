10 DIMA(38),B(38),C(46)
20 CLS:PRINT@11,"SPACE DUEL"
30 PRINT:PRINT" -LEFT PLAYER SHOOTS WITH 'W'    AND MOVES U/D WITH 'Q' & 'A'."
40 PRINT:PRINT" -RIGHT PLAYER SHOOTS WITH '0'   AND MOVES U/D WITH 1 'P' &';'."
50 PRINT:PRINT" -JOYSTICKS MOVE PLAYER U/D AND  FIRE WITH BUTTONS."
60 PRINT:PRINT"EACH PLAYER GETS 7 SHOTS. HOLD  FIRE  DOWN UNTIL LASER HEATS UP,THEN LET GO TO FIRE.":PRINT:PRINT"<HIT ANY KEY TO CONTINUE>";
70 IF INKEY$="" THEN 70
80 CLS:   PRINT" -WHEN PLAYING AGAINST THE       COMPUTER, IT GETS AN UNLIMITED  NUMBER OF SHOTS."
90 PRINT: PRINT" -A DRAW IS AWARDED IN           THE EVENT THAT BOTH PLAYERS     RUN OUT OF LASER SHOTS."
100 PRINT:PRINT" -HOLDING FIRE BUTTON DOWN WILL  EXPELL LASER FIRE A FEW STEPS   BUT WILL NOT BE COUNTED AS A    SHOT FIRED.":PRINT:PRINT"<HIT ANY KEY>";
110 IF INKEY$=""THEN110
120 CLSRND(8):PLAY"T19FE04FED03FEDG02FEDGD01FEDGDC03FEDCGF02DE01DE03DE04DE02DEFFD01C03DT255"
130 CLS0:PMODE4,1:PCLS
140 DRAW"S4BM34,100;G4D3F1R1D1R3U1H2U2E1D3F1U5F1D4E1R1H1U1D3G2R2"
150 DRAW"D1L5G2D5F2G1D1R9U1L8E1R5U1"
160 DRAW"E2U1H1R3BR2U1NR2BL2L3E1H2U1E2U3H4NE4L2H4BD16BR3D5R4"
170 DRAW"BM222,100;F4D3G1L1D1L3U1E2U2H1D3G1U5G1D4H1L1E1U1D3F2L2"
180 DRAW"D1R5F2D5G2F1D1L9U1R8H1L5U1"
190 DRAW"H2U1E1L3BL2U1NL2BR2R3H1E2U1H2U3E4NH4R2E4BD16BL3ND2L4D5BR4H3R3"
200 DRAW"S8BM126,93;D5G8F2R1NF1BE1NR2U3R2BR2NR2D3E1F1U3BR2ND3R2D3BD2BL10R1F1R6E1R1E1R1E2H8U5L2"
210 GET(30,88)-(46,134),A,G
220 GET(210,88)-(226,134),B,G
230 GET(110,88)-(148,134),C,G
240 CIRCLE(70,161),8:PAINT(70,161),5,5
250 CIRCLE(186,33),8:PAINT(186,33),5,5
260 CLS:INPUT"1 OR 2 PLAYERS";C:IFC=1THENINPUT"DIFFICULTY (1-4)";D:IFD<1ORD>4THEN260
270 IFC>2ORC<1THEN260
280 CLS:INPUT"JOYSTICKS (Y/N)";A$
290 LX=88:RX=88:SX=88:B=22:SL=0:SR=0:LC=0:RC=0
300 SCREEN1,1:IF A$<>"Y" THEN 400
310 A=JOYSTK(0):A=JOYSTK(3)
320 IFC=2THENB=JOYSTK(1)
330 IFA<21THENLU=-8
340 IFA>42THENLU=8
350 IFB<21THENRU=-8
360 IFB>42THENRU=8
370 P2=PEEK(65280):IFP2=125ORP2=253THENSL=1:LQ=47:LZ=LX+25
380 IFC=2THENIFP2=254ORP2=126THENSR=1:RQ=209:RZ=RX+25
390 GOSUB470:GOTO310
400 IFPEEK(339)=254THENLU=8
410 IFPEEK(339)=251THENLU=-8
420 IFC<>2THEN440
421 IFPEEK(338)=251THENRU=-8
430 IFPEEK(341)=223THENRU=8
440 IFPEEK(345)=251THENSL=1:LQ=47:LZ=LX+25
450 IFPEEK(345)=253THENSR=1:RQ=209:RZ=RX+25
460 GOSUB470:GOTO400
470 IFC=2THENIFRU GOSUB570
480 IFLU GOSUB550
490 IFC=1GOSUB710
500 IFRND(10)=5GOSUB680
510 IFLC>6THENIFRC>6THENCLS:PRINT"'DRAW.'":FORI=1TO500:NEXT:GOTO790
520 IFSL THENIFLC<7GOSUB590:GOSUB590:GOSUB590
530 IFC=2 THENIFSR THENIFRC<7GOSUB630:GOSUB630:GOSUB630
540 RETURN
550 IFLX+LU<0ORLX+LU>148THENRETURN
560 LX=LX+LU:PUT(30,LX)-(46,LX+46),A,PSET:LU=0:RETURN
570 IFRX+RU<0ORRX+RU>148THENRETURN
580 RX=RX+RU:PUT(210,RX)-(226,RX+46),B,PSET:RU=0:RETURN
590 IFSL=0THENRETURNELSEIFLQ+20>255ORPPOINT(LQ+20,LZ)OR LQ+20>110THENIFLZ>SX+4THENIFLZ<SX+41THENPLAY"D":SL=0:LC=LC+1:RETURN
600 LINE(LQ,LZ)-(LQ+20,LZ),PSET:LINE(LQ,LZ)-(LQ+20,LZ),PRESET
610 LQ=LQ+20:IFLQ>209THENIFLZ>RX+12THENIFLZ<RX+34THENPLAY"AEGCFFED":FORI=1TO10:PUT(210,RX+I)-(227,RX+46+I),B,PSET:PLAY"CFDE":NEXT:W$="LEFT":GOTO780
620 RETURN
630 IFSR=0THENRETURNELSEIFRQ-20<0ORRQ-20<148ANDRZ<SX+41ANDRZ>SX+4THENPLAY"F":SR=0:RC=RC+1:RETURN
640 IFPPOINT(RQ-20,RZ)THENPLAY"F":SR=0:RC=RC+1:RETURN
650 LINE(RQ,RZ)-(RQ-20,RZ),PSET:LINE(RQ,RZ)-(RQ-20,RZ),PRESET
660 RQ=RQ-20:IFRQ<47THENIFRZ>LX+12THENIFRZ<LX+34THENPLAY"DEFFCGEA":FORI=1TO10:PUT(30,LX+I)-(47,LX+46+I),A,PSET:PLAY"FCD":NEXT:W$="RIGHT":GOTO780
670 RETURN
680 SU=5*(2-RND(3)):IFSU=0GOTO680
690 IFSX+SU<30ORSX+SU>133THENRETURN
700 SX=SX+SU:PUT(110,SX)-(148,SX+46),C,PSET:RETURN
710 IFLX<RX THENRX=RX-D*2ELSEIFLX>RX THENRX=RX+D*2
720 IFRX<0THENRX=0ELSEIFRX>148THENRX=148
730 GOSUB580
740 IFSS THENIFRX+25>LX+8THENIFRX+25<LX+26THENSS=0:SR=1:RQ=209:RZ=RX+25
750 IFSS=0THENFORI=1TOD:GOSUB630:NEXT
760 IFSR=0THENSS=1
770 RETURN
780 CLS:PRINTW$"HAND PLAYER WON.":FORI=1TO1000:NEXT
790 CLS:PRINT"PLAY AGAIN (Y/N)?"
800 A$=INKEY$:IFA$="Y"THEN130 ELSE IF A$="N"THEN CLS:PRINT"BYE.":END
810 GOTO800
